# Rule_ID:214
# @title [Dataset] dct:title should have a single value per a language tag
# @tag Dataset

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>

SELECT ?Class_Name ?Rule_ID ?Rule_Severity ?Rule_Description ?Message ?s ?p ?o  ?Rule_Auxillary
WHERE {
  ?s a dcat:Dataset.
   { select ?s count ( ?oo ) as ?c  ?lang where {
       ?s a dcat:Dataset.
       ?s dct:title ?oo.
       FILTER(isLiteral(?oo)).
       BIND (lang(?oo) as ?lang).
       } group by ?s ?lang
}
  FILTER (?c > 1).
  BIND (?lang as ?o ).
  BIND (dct:title AS ?p).
  BIND ("Dataset" AS ?Class_Name).
  BIND (214 AS ?Rule_ID).
  BIND ("warning" AS ?Rule_Severity).
  BIND ("dct:title should have just one value per language tag." AS ?Rule_Description).
  BIND (concat("The dct:title ",str(?s)," has more than one value per language tag ",str(?lang)) AS ?Message).
  BIND ("vodap" AS ?Rule_Auxillary).
}

